#include "MAPL_Generic.h"

!=============================================================================
!BOP

! !MODULE: DU2G_GridCompMod - GOCART refactoring of the DU gridded component 

! !INTERFACE:
module DU2G_GridCompMod

!  !USES:
   use ESMF
   use MAPL
   use Chem_MieTableMod2G
   use Chem_AeroGeneric
   use iso_c_binding, only: c_loc, c_f_pointer, c_ptr

   use Chem_UtilMod          
   use GOCART2G_Process       ! GOCART2G process library

   
   implicit none
   private

   integer, parameter :: instanceComputational = 1
   integer, parameter :: instanceData          = 2

! !PUBLIC MEMBER FUNCTIONS:
   public  SetServices


! !DESCRIPTION: This module implements GOCART2G's Dust (DU) Gridded Component.

! !REVISION HISTORY:
! 16Oct2019  Sherman, da Silva, Darmenov, Clune -  First attempt at refactoring.

!EOP
!===========================================================================

   integer, parameter         :: NHRES = 6  ! DEV NOTE!!! should this be allocatable, and not a parameter?
!  !Dust state
   type DU2G_GridComp
       type(Chem_Mie), dimension(2)    :: rad_MieTable, diag_MieTable
       real, allocatable      :: radius(:)      ! particle effective radius [um]
       real, allocatable      :: rlow(:)        ! particle effective radius lower bound [um]
       real, allocatable      :: rup(:)         ! particle effective radius upper bound [um]
       real, allocatable      :: sfrac(:)       ! fraction of total source
       real, allocatable      :: rhop(:)        ! soil class density [kg m-3]
       real                   :: Ch_DU_res(NHRES) ! resolutions used for Ch_DU
       real                   :: Ch_DU          ! dust emission tuning coefficient [kg s2 m-5].
       real, allocatable      :: fscav(:)       ! scavenging efficiency
       real, allocatable      :: molwght(:)     ! molecular weight
       real, allocatable      :: fnum(:)        ! number of particles per kg mass
       integer                :: rhFlag
       logical                :: maringFlag=.false.  ! maring settling velocity correction
       integer                :: nbins
       integer                :: km             ! vertical grid dimension
       real                   :: CDT            ! chemistry timestep (secs)
       integer                :: instance       ! data or computational instance
       integer                :: day_save = -1      
!       character (len=ESMF_MAXSTR), allocatable    :: aerosol_names(:)
!      !Workspae for point emissions
       logical                :: doing_point_emissions = .false.
       character(len=255)     :: point_emissions_srcfilen   ! filename for pointwise emissions
       integer                         :: nPts = -1
       integer, pointer, dimension(:)  :: pstart => null(), pend => null()
       real, pointer, dimension(:)     :: pLat  => null(), &
                                          pLon  => null(), &
                                          pBase => null(), &
                                          pTop  => null(), &
                                          pEmis => null()
   end type DU2G_GridComp

   type wrap_
      type (DU2G_GridComp), pointer     :: PTR => null()
   end type wrap_

contains

!============================================================================
!BOP

! !IROUTINE: SetServices 

! !INTERFACE:
  subroutine SetServices (GC, RC)

!   !ARGUMENTS:
    type (ESMF_GridComp), intent(INOUT)   :: GC  ! gridded component
    integer,              intent(  OUT)   :: RC  ! return code

!   !DESCRIPTION: This version uses MAPL_GenericSetServices, which sets
!     the Initialize and Finalize services to generic versions. It also
!     allocates our instance of a generic state and puts it in the 
!     gridded component (GC). Here we only set the two-stage run method
!     and declare the data services.

!   !REVISION HISTORY: 
!   16oct2019   E.Sherman  First attempt at refactoring

!EOP
!============================================================================

!
!   !Locals
    character (len=ESMF_MAXSTR)                 :: COMP_NAME
    type (ESMF_Config)                          :: cfg
    type (wrap_)                                :: wrap
    type (DU2G_GridComp), pointer               :: self
    type (Chem_Mie)                             :: this

    character (len=ESMF_MAXSTR)                 :: field_name
    character (len=ESMF_MAXSTR), allocatable    :: aerosol_names(:)

    integer                                     :: n, i, nCols, nbins
    real                                        :: DEFVAL
    logical                                     :: data_driven = .true.
!    integer, parameter                          :: bins = 5  ! This should equal the number of bins. Is this how we want to handle this?

    __Iam__('SetServices')

!****************************************************************************
!   Begin...

!   Get my name and set-up traceback handle
!   ---------------------------------------
    call ESMF_GridCompGet (GC, NAME=COMP_NAME, __RC__)
    Iam = trim(COMP_NAME) //'::'// Iam

if (mapl_am_I_root()) print*,' test DU2G SetServices COMP_NAME = ',trim(COMP_NAME)


!   Wrap internal state for storing in GC
!   -------------------------------------
    allocate (self, __STAT__)
    wrap%ptr => self

!   Load resource file  
!   -------------------
    cfg = ESMF_ConfigCreate (__RC__)
    call ESMF_ConfigLoadFile (cfg, 'DU2G_GridComp_'//trim(COMP_NAME)//'.rc', rc=status)
    if (status /= 0) then
        if (mapl_am_i_root()) print*,'DU2G_GridComp_'//trim(COMP_NAME)//'.rc does not exist! Loading DU2G_GridComp_DU.rc instead'
        call ESMF_ConfigLoadFile (cfg, 'DU2G_GridComp_DU.rc', __RC__)
    end if

!   Get nbins from cfg
    call ESMF_ConfigGetAttribute (cfg, self%nbins, label='nbins:', __RC__)
    nbins = self%nbins

!   Parse config file into private internal state
!   ----------------------------------------------
    allocate(self%radius(nbins), self%rlow(nbins), self%rup(nbins), self%sfrac(nbins), &
             self%rhop(nbins), self%fscav(nbins), self%molwght(nbins), self%fnum(nbins), &
             __STAT__)

!    self%aerosol_names = aerosol_names

    call ESMF_ConfigGetAttribute (cfg, self%radius,     label='particle_radius_microns:', __RC__)
    call ESMF_ConfigGetAttribute (cfg, self%rlow,       label='radius_lower:', __RC__)
    call ESMF_ConfigGetAttribute (cfg, self%rup,        label='radius_upper:', __RC__)
    call ESMF_ConfigGetAttribute (cfg, self%sfrac,      label='source_fraction:', __RC__)
    call ESMF_ConfigGetAttribute (cfg, self%rhop,       label='soil_density:', __RC__)
    call ESMF_ConfigGetAttribute (cfg, self%Ch_DU_res,  label='Ch_DU:', __RC__)
    call ESMF_ConfigGetAttribute (cfg, self%fscav,      label='fscav:', __RC__)
    call ESMF_ConfigGetAttribute (cfg, self%molwght,    label='molecular_weight:', __RC__)
    call ESMF_ConfigGetAttribute (cfg, self%fnum,       label='fnum:', __RC__)
    call ESMF_ConfigGetAttribute (cfg, self%rhFlag,     label='rhFlag:', __RC__)
    call ESMF_ConfigGetAttribute (cfg, self%maringFlag, label='maringFlag:', __RC__)
    call ESMF_ConfigGetAttribute (cfg, self%point_emissions_srcfilen, &
                                  label='point_emissions_srcfilen:', default='/dev/null', __RC__)


!   Is DU data driven?
!   ------------------
    call determine_data_driven (COMP_NAME, data_driven, __RC__)

!   Set entry points
!   ------------------------
    call MAPL_GridCompSetEntryPoint (GC, ESMF_Method_Initialize,  Initialize, __RC__)
    call MAPL_GridCompSetEntryPoint (GC, ESMF_Method_Run, Run, __RC__)
    if (data_driven /= .true.) then
       call MAPL_GridCompSetEntryPoint (GC, ESMF_Method_Run, Run2, __RC__)
    end if

!   INTERNAL STATE
!   ---------------
!   Default internal state values
!   -----------------------------
    DEFVAL = 0.0

#include "DU2G_Internal___.h"

!   IMPORT STATE
!   -------------
    if (data_driven) then
       do i = 1, nbins 
          write (field_name, '(A, I0.3)') '', i
          call MAPL_AddImportSpec(GC,                                     &
             short_name = 'climdu'//trim(field_name),                        &
             long_name  = 'Dust Mixing Ratio (bin '//trim(field_name)//')',  &
             units      = 'kg kg-1',                                         &
             restart    = MAPL_RestartSkip,                                  &
             dims       = MAPL_DimsHorzVert,                                 &
             vlocation  = MAPL_VLocationCenter, __RC__)

!        !dry deposition
          call MAPL_AddImportSpec(GC,                                       &
             short_name = 'climDUDP'//trim(field_name),                      &
             long_name  = 'Dust Mixing Ratio (bin '//trim(field_name)//')',  &
             units      = 'kg kg-1',                                         &
             dims       = MAPL_DimsHorzOnly,                                 &
             vlocation  = MAPL_VLocationCenter,                              &
             restart    = MAPL_RestartSkip, __RC__)

!        !wet deposition    
          call MAPL_AddImportSpec(GC,                                       &
             short_name = 'climDUWT'//trim(field_name),                     &
             long_name  = 'Dust Mixing Ratio (bin '//trim(field_name)//')', &
             units      = 'kg kg-1',                                        &
             dims       = MAPL_DimsHorzOnly,                                &
             vlocation  = MAPL_VLocationCenter,                             &
             restart    = MAPL_RestartSkip, __RC__)

!        !gravitational settling
          call MAPL_AddImportSpec(GC,                                       &
             short_name = 'climDUSD'//trim(field_name),                     &
             long_name  = 'Dust Mixing Ratio (bin '//trim(field_name)//')', &
             units      = 'kg kg-1',                                        &
             dims       = MAPL_DimsHorzOnly,                                &
             vlocation  = MAPL_VLocationCenter,                             &
             restart    = MAPL_RestartSkip, __RC__)

!        !convective scavenging
          call MAPL_AddImportSpec(GC,                                       &
             short_name = 'climDUSV'//trim(field_name),                     &
             long_name  = 'Dust Mixing Ratio (bin '//trim(field_name)//')', &
             units      = 'kg kg-1',                                        &
             dims       = MAPL_DimsHorzOnly,                                &
             vlocation  = MAPL_VLocationCenter,                             &
             restart    = MAPL_RestartSkip, __RC__)
        end do
    end if ! (data_driven)


!   EXPORT STATE
!   -------------
    if (.not. data_driven) then
#include "DU2G_Export___.h"
#include "DU2G_Import___.h"
    end if


!   This state holds fields needed by radiation
!   ---------------------------------------------
    call MAPL_AddExportSpec (GC,                             &
       short_name = trim(COMP_NAME)//'_AERO',                &
       long_name  = 'aerosols_from_'//trim(COMP_NAME),       &
       units      = 'kg kg-1',                               &
       dims       = MAPL_DimsHorzVert,                       &
       vlocation  = MAPL_VLocationCenter,                    &
       datatype   = MAPL_StateItem, __RC__)

!   This state is needed by MOIST - It will contain aerosols
!   ----------------------------------------------------------
    call MAPL_AddExportSpec (GC,                                                  &
       short_name = trim(COMP_NAME)//'_AERO_ACI',                                 &
       long_name  = 'aerosol_cloud_interaction_aerosols_from_'//trim(COMP_NAME),  &
       units      = 'kg kg-1',                                                    &
       dims       = MAPL_DimsHorzVert,                                            &
       vlocation  = MAPL_VLocationCenter,                                         &
       datatype   = MAPL_StateItem, __RC__)

!   This bundle is needed by surface for snow albedo modification
!   by aerosol settling and deposition
!   ~~~DEVELOPERS NOTE~~~ Change to StateItem when possible
!                         This will require refactoring Radiation
!   ---------------------------------------------------------------
    call MAPL_AddExportSpec (GC,                                  &
       short_name = trim(COMP_NAME)//'_AERO_DP',                                    &
       long_name  = 'aerosol_deposition_from_'//trim(COMP_NAME),  &
       units      = 'kg m-2 s-1',                                 &
       dims       = MAPL_DimsHorzOnly,                            &
       datatype   = MAPL_BundleItem, __RC__)



!  Store internal state in GC
!  --------------------------
   call ESMF_UserCompSetInternalState ( GC, 'DU2G_GridComp', wrap, STATUS )
   VERIFY_(STATUS)

!   Set generic services
!   ----------------------------------
    call MAPL_GenericSetServices (GC, __RC__)


    RETURN_(ESMF_SUCCESS)

  end subroutine SetServices

!============================================================================
!BOP

! !IROUTINE: Initialize 

! !INTERFACE:
  subroutine Initialize (GC, import, export, clock, RC)

!   !ARGUMENTS:
    type (ESMF_GridComp), intent(inout) :: GC     ! Gridded component 
    type (ESMF_State),    intent(inout) :: import ! Import state
    type (ESMF_State),    intent(inout) :: export ! Export state
    type (ESMF_Clock),    intent(inout) :: clock  ! The clock
    integer, optional,    intent(  out) :: RC     ! Error code

! !DESCRIPTION: This initializes DU's Grid Component. It primaryily fills 
!               GOCART's AERO states with its dust fields. 

! !REVISION HISTORY: 
! 16oct2019   E.Sherman  First attempt at refactoring

!EOP
!============================================================================
!   !Locals
    character (len=ESMF_MAXSTR)          :: COMP_NAME
    type (MAPL_MetaComp),       pointer  :: MAPL
    type (ESMF_Grid)                     :: grid
    type (ESMF_State)                    :: internal
    type (ESMF_State)                    :: aero, aero_aci
    type (ESMF_State)                    :: providerState
    type (ESMF_Config)                   :: cfg
    type (ESMF_FieldBundle)              :: Bundle_DP
    type (wrap_)                         :: wrap
    type (DU2G_GridComp), pointer        :: self

    integer, allocatable                 :: mieTable_pointer(:)
    integer                              :: i, nbins, nCols, dims(3), km
    integer                              :: instance
    type (ESMF_Field)                    :: field, fld
    character (len=ESMF_MAXSTR)          :: field_name, prefix
!    character (len=ESMF_MAXSTR), allocatable   :: aerosol_names(:)
    real                                 :: CDT         ! chemistry timestep (secs)
    integer                              :: HDT         ! model     timestep (secs)

    logical                              :: data_driven
    integer                              :: NUM_BANDS

    __Iam__('Initialize')

!****************************************************************************

!   Begin... 

!   Get the target components name and set-up traceback handle.
!   -----------------------------------------------------------
    call ESMF_GridCompGet (GC, grid=grid, name=COMP_NAME, __RC__)
    Iam = trim(COMP_NAME) // '::' //trim(Iam)

!   Get my internal MAPL_Generic state
!   -----------------------------------
    call MAPL_GetObjectFromGC (GC, MAPL, __RC__)

!   Get my internal state
!   ---------------------
    call ESMF_UserCompGetInternalState(GC, 'DU2G_GridComp', wrap, STATUS)
    VERIFY_(STATUS)
    self => wrap%ptr

    call MAPL_GridGet ( grid, globalCellCountPerDim=dims, __RC__ )

!   Dust emission tuning coefficient [kg s2 m-5]. NOT bin specific.
!   ---------------------------------------------------------------
    self%Ch_DU = Chem_UtilResVal(dims(1), dims(2), self%Ch_DU_res(:), __RC__)
    self%Ch_DU = self%Ch_DU * 1.00E-09

!   Get dimensions
!   ---------------
    km = dims(3)
    self%km = km

!   Get DTs
!   -------
    call MAPL_GetResource(mapl, HDT, Label='RUN_DT:', __RC__)                        
    call MAPL_GetResource(mapl, CDT, Label='GOCART_DT:', default=real(HDT), __RC__)
    self%CDT = CDT

!   Load resource file  
!   -------------------
    cfg = ESMF_ConfigCreate (__RC__)
    call ESMF_ConfigLoadFile (cfg, 'DU2G_GridComp_'//trim(COMP_NAME)//'.rc', RC=STATUS)
    if (STATUS /= 0) then
        if (mapl_am_i_root()) print*,'DU2G_GridComp_'//trim(COMP_NAME)//'.rc does not exist! &
                                      loading DU2G_GridComp_DU.rc instead'
        call ESMF_ConfigLoadFile (cfg, 'DU2G_GridComp_DU.rc', __RC__)
    end if

!   Call Generic Initialize 
!   ------------------------
    call MAPL_GenericInitialize (GC, import, export, clock, __RC__)

!   Get parameters from generic state.
!   -----------------------------------
    call MAPL_Get (MAPL, INTERNAL_ESMF_STATE=internal, __RC__)

!   Is DU data driven?
!   ------------------
    call determine_data_driven (COMP_NAME, data_driven, __RC__)

!   If this is a data component, the data is provided in the import
!   state via ExtData instead of the actual GOCART children
!   ---------------------------------------------------------------- 
    if ( data_driven ) then
        providerState = import
        prefix = 'clim'
    else
        providerState = export
        prefix = ''
    end if

if(mapl_am_i_root()) print*,'DU2G INIT data_driven = ',data_driven

!   Fill AERO States with dust fields
!   ------------------------------------
    call ESMF_StateGet (export, trim(COMP_NAME)//'_AERO'    , aero    , __RC__)
    call ESMF_StateGet (export, trim(COMP_NAME)//'_AERO_ACI', aero_aci, __RC__)
    call ESMF_StateGet (export, trim(COMP_NAME)//'_AERO_DP' , Bundle_DP, __RC__)

    call ESMF_StateGet (internal, 'DU', field, __RC__)
    fld = MAPL_FieldCreate (field, 'DU', __RC__)
    call MAPL_StateAdd (aero, fld, __RC__)
    call MAPL_StateAdd (aero_aci, fld, __RC__)

!   Dry deposition
!   ---------------
    call append_to_bundle('DUDP', providerState, prefix, Bundle_DP, __RC__)

!   Wet deposition (Convective scavenging)
!   --------------------------------------
    call append_to_bundle('DUSV', providerState, prefix, Bundle_DP, __RC__)

!   Wet deposition
!   ---------------
    call append_to_bundle('DUWT', providerState, prefix, Bundle_DP, __RC__)

!   Gravitational Settling
!   ----------------------
    call append_to_bundle('DUSD', providerState, prefix, Bundle_DP, __RC__)

!   Set AERO States' attributes
!   ----------------------------
    if (data_driven) then
       instance = instanceData
    else
       instance = instanceComputational
    end if

    self%instance = instance

!   Create Radiation Mie Table
!   --------------------------
    call MAPL_GetResource (MAPL, NUM_BANDS, 'NUM_BANDS:', __RC__)

!   Get file names for the optical tables
    call ESMF_ConfigGetAttribute (cfg, self%rad_MieTable(instance)%optics_file, label="aerosol_optics:", __RC__ )

    allocate (self%rad_MieTable(instance)%channels(NUM_BANDS), __STAT__ )

    call ESMF_ConfigGetAttribute (cfg, self%rad_MieTable(instance)%channels, label= "BANDS:", &
                                 count=self%rad_MieTable(instance)%nch, rc=status)

if(mapl_am_i_root()) print*,'DU2G channels = ',self%rad_MieTable(instance)%channels

    if (rc /= 0) then
       do i = 1, NUM_BANDS
          self%rad_MieTable(instance)%channels(i) = i
       end do
    end if

if(mapl_am_i_root()) print*,'DU2G size(channels) = ',size(self%rad_MieTable(instance)%channels)
    
    allocate (self%rad_MieTable(instance)%mie_aerosol, __STAT__)
    self%rad_MieTable(instance)%mie_aerosol = Chem_MieTableCreate (self%rad_MieTable(instance)%optics_file, rc)
    call Chem_MieTableRead (self%rad_MieTable(instance)%mie_aerosol, NUM_BANDS, self%rad_MieTable(instance)%channels, rc)


!   Create Diagnostics Mie Table
!   -----------------------------
!   Get file names for the optical tables
    call ESMF_ConfigGetAttribute (cfg, self%diag_MieTable(instance)%optics_file, label="diagnostic_optics:", __RC__ )
    call ESMF_ConfigGetAttribute (cfg, self%diag_MieTable(instance)%nch, label="n_channels:", __RC__)
    call ESMF_ConfigGetAttribute (cfg, self%diag_MieTable(instance)%nmom, label="n_moments:", default=0,  __RC__)
    allocate (self%diag_MieTable(instance)%channels(self%diag_MieTable(instance)%nch), __STAT__ )
    call ESMF_ConfigGetAttribute (cfg, self%diag_MieTable(instance)%channels, label= "r_channels:", __RC__)

    allocate (self%diag_MieTable(instance)%mie_aerosol, __STAT__)
    self%diag_MieTable(instance)%mie_aerosol = Chem_MieTableCreate (self%diag_MieTable(instance)%optics_file, __RC__ )
    call Chem_MieTableRead (self%diag_MieTable(instance)%mie_aerosol, self%diag_MieTable(instance)%nch, &
                            self%diag_MieTable(instance)%channels, rc, nmom=self%diag_MieTable(instance)%nmom)

!if(mapl_am_i_root()) print*,'DU2G rchannels = ',self%diag_MieTable(instance)%channels

!   Mie Table instance/index
    call ESMF_AttributeSet (aero, name='mie_table_instance', value=instance, __RC__)

!   Add variables to DU instance's aero state. This is used in aerosol optics calculations
    call add_aero (aero, label='air_pressure_for_aerosol_optics',             label2='PLE', grid=grid, typekind=MAPL_R4, __RC__)
    call add_aero (aero, label='relative_humidity_for_aerosol_optics',        label2='RH',  grid=grid, typekind=MAPL_R4, __RC__)
!   call ESMF_StateGet (import, 'PLE', field, __RC__)
!   call MAPL_StateAdd (aero, field, __RC__)
!   call ESMF_StateGet (import, 'RH2', field, __RC__)
!   call MAPL_StateAdd (aero, field, __RC__)

    call add_aero (aero, label='extinction_in_air_due_to_ambient_aerosol',    label2='EXT', grid=grid, typekind=MAPL_R8, __RC__)
    call add_aero (aero, label='single_scattering_albedo_of_ambient_aerosol', label2='SSA', grid=grid, typekind=MAPL_R8, __RC__)
    call add_aero (aero, label='asymmetry_parameter_of_ambient_aerosol',      label2='ASY', grid=grid, typekind=MAPL_R8, __RC__)

    call ESMF_AttributeSet(aero, name='band_for_aerosol_optics',             value=0,     __RC__)

    mieTable_pointer = transfer(c_loc(self), [1])

    call ESMF_AttributeSet(aero, name='mieTable_pointer', valueList=mieTable_pointer, itemCount=size(mieTable_pointer), __RC__)

    ! attach the aerosol optics method
    call ESMF_MethodAdd (aero, label='aerosol_optics', userRoutine=aerosol_optics, __RC__)

if (mapl_am_I_root()) print*,trim(comp_name),' INIT END'

    RETURN_(ESMF_SUCCESS)

  end subroutine Initialize


!============================================================================
!BOP
! !IROUTINE: Run 

! !INTERFACE:
  subroutine Run (GC, import, export, clock, rc)

!   !ARGUMENTS:
    type (ESMF_GridComp), intent(inout) :: GC     ! Gridded component 
    type (ESMF_State),    intent(inout) :: import ! Import state
    type (ESMF_State),    intent(inout) :: export ! Export state
    type (ESMF_Clock),    intent(inout) :: clock  ! The clock
    integer, optional,    intent(  out) :: rc     ! Error code:

!   !DESCRIPTION: Run method for the Dust Grid Component. Determines whether to
!                 run data or computational run method.

!EOP
!============================================================================
!   !Locals
    character (len=ESMF_MAXSTR)       :: COMP_NAME
    type (MAPL_MetaComp), pointer     :: MAPL
    type (ESMF_State)                 :: internal

    logical                           :: data_driven

    __Iam__('Run')

!*****************************************************************************
!   Begin... 

!   Get my name and set-up traceback handle
!   ---------------------------------------
    call ESMF_GridCompGet (GC, NAME=COMP_NAME, __RC__)
    Iam = trim(COMP_NAME) //'::'// Iam

if (mapl_am_I_root()) print*,trim(comp_name),' Run BEGIN'


!   Get my internal MAPL_Generic state
!   -----------------------------------
    call MAPL_GetObjectFromGC (GC, MAPL, __RC__)

!   Get parameters from generic state.
!   -----------------------------------
    call MAPL_Get (MAPL, INTERNAL_ESMF_STATE=internal, __RC__)

!   Is DU data driven?
!   ------------------
    call determine_data_driven (COMP_NAME, data_driven, __RC__)

!   Update INTERNAL state variables with ExtData
!   ---------------------------------------------
    if (data_driven) then
       call Run_data (GC, import, export, internal, __RC__)
    else
       call Run1 (GC, import, export, clock, __RC__)
    end if

if (mapl_am_I_root()) print*,trim(comp_name),' Run END'

    RETURN_(ESMF_SUCCESS)

  end subroutine Run

!============================================================================
!BOP
! !IROUTINE: Run1 

! !INTERFACE:
  subroutine Run1 (GC, import, export, clock, RC)

!   !ARGUMENTS:
    type (ESMF_GridComp), intent(inout) :: GC     ! Gridded component 
    type (ESMF_State),    intent(inout) :: import ! Import state
    type (ESMF_State),    intent(inout) :: export ! Export state
    type (ESMF_Clock),    intent(inout) :: clock  ! The clock
    integer, optional,    intent(  out) :: RC     ! Error code:

! !DESCRIPTION:  Computes emissions/sources for Dust

!EOP
!============================================================================
!   !Locals
    character (len=ESMF_MAXSTR)       :: COMP_NAME
    type (MAPL_MetaComp), pointer     :: mapl
    type (ESMF_State)                 :: internal
    type (ESMF_Grid)                  :: grid
    type (wrap_)                      :: wrap
    type (DU2G_GridComp), pointer     :: self
    type(ESMF_Time)                   :: time

    integer                             :: nymd, nhms, iyr, imm, idd, ihr, imn, isc
    integer                             :: import_shape(2), i2, j2
    real, pointer, dimension(:,:,:)     :: ptr3d_int
    real, pointer, dimension(:,:)     :: emissions_surface
    real, dimension(:,:,:,:), allocatable :: emissions
    real, dimension(:,:,:), allocatable :: emissions_point

    integer, pointer, dimension(:)  :: iPoint, jPoint
    integer :: i, n, kmin
    real, parameter ::  UNDEF  = 1.e15 
    real, parameter ::  grav   = 9.80616

#include "DU2G_DeclarePointer___.h"

   __Iam__('Run1')

!*****************************************************************************
!   Begin... 

!   Get my name and set-up traceback handle
!   ---------------------------------------
    call ESMF_GridCompGet (GC, grid=grid, NAME=COMP_NAME, __RC__)
    Iam = trim(COMP_NAME) //'::'// Iam

!if(mapl_am_i_root()) print*,'DU2G Run1 BEGIN'

!   Get my internal MAPL_Generic state
!   -----------------------------------
    call MAPL_GetObjectFromGC (GC, mapl, __RC__)

!   Get parameters from generic state.
!   -----------------------------------
    call MAPL_Get (mapl, INTERNAL_ESMF_STATE=internal, __RC__)

#include "DU2G_GetPointer___.h"

!   Set du_src to 0 where undefined
!   --------------------------------
    where (1.01*du_src > UNDEF) du_src = 0.

!   Get my private internal state
!   ------------------------------
    call ESMF_UserCompGetInternalState(GC, 'DU2G_GridComp', wrap, STATUS)
    VERIFY_(STATUS)
    self => wrap%ptr

!   Extract nymd(yyyymmdd) from clock
!   ---------------------------------
    call ESMF_ClockGet (clock, currTime=time, __RC__)
    call ESMF_TimeGet (time ,YY=iyr, MM=imm, DD=idd, H=ihr, M=imn, S=isc, __RC__)
    call MAPL_PackTime (nymd, iyr, imm , idd)
    call MAPL_PackTime (nhms, ihr, imn, isc)

!   Get dimensions
!   ---------------
    import_shape = shape(wet1)
    i2 = import_shape(1)
    j2 = import_shape(2)

    allocate(emissions(i2,j2,self%km,self%nbins),  __STAT__)
    emissions = 0.0
    allocate(emissions_point, mold=delp,  __STAT__)
    emissions_point = 0.0
    allocate(emissions_surface(i2,j2), __STAT__)
    emissions_surface = 0.0


do n = 1, self%nbins
 if(mapl_am_i_root()) print*,'n = ', n,' : DU2G sum(du00n) = ',sum(DU(:,:,:,n))
end do


!   Get surface gridded emissions
!   -----------------------------
    call DustEmissionGOCART2G(self%radius*1.e-6, frlake, wet1, lwi, u10m, v10m, &
                              self%Ch_DU, du_src, MAPL_GRAV, &
                              emissions_surface, __RC__)

!   Read point emissions file once per day
!   --------------------------------------
    if (self%day_save /= idd) then
        self%day_save = idd
        call Chem_UtilPointEmissions( nymd, self%point_emissions_srcfilen, &
                                      self%nPts, self%pLat, self%pLon, &
                                      self%pBase, self%pTop, self%pEmis, &
                                      self%pStart, self%pEnd )

!       In case pStart or pEnd were not specified in the file set to defaults
        where(self%pStart < 0) self%pStart = 000000
        where(self%pEnd < 0)   self%pEnd   = 240000
    end if

!   Get indices for point emissions
!   -------------------------------
    if (self%nPts > 0) then
        allocate(iPoint(self%nPts), jPoint(self%nPts),  __STAT__)
        call MAPL_GetHorzIJIndex(self%nPts, iPoint, jPoint, &
                                 grid = grid,               &
                                 lon  = self%pLon/real(MAPL_RADIANS_TO_DEGREES), &
                                 lat  = self%pLat/real(MAPL_RADIANS_TO_DEGREES), &
                                 rc   = status)
            if ( status /= 0 ) then
                if (mapl_am_i_root()) print*, trim(Iam), ' - cannot get indices for point emissions'
                VERIFY_(status)
            end if

        call updatePointwiseEmissions (self%km, self%pBase, self%pTop, self%pEmis, self%nPts, &
                                       self%pStart, self%pEnd, airdens, delp, MAPL_GRAV, &
                                       area, iPoint, jPoint, nhms, emissions_point, __RC__)
    end if

!   Update aerosol state
!   --------------------
    call UpdateAerosolState (emissions, emissions_surface, emissions_point, &
                             self%sfrac, self%nPts, self%km, self%CDT, MAPL_GRAV, &
                             self%nbins, delp, DU, rc)


    if (associated(DUEM)) then
       DUEM = sum(emissions, dim=3)
    end if

!   Clean up
!   --------
    deallocate(emissions, emissions_surface, emissions_point, __STAT__)
    if (self%nPts > 0) then
        deallocate(iPoint, jPoint, __STAT__)
    end if

    RETURN_(ESMF_SUCCESS)

  end subroutine Run1

!============================================================================
!BOP
! !IROUTINE: Run2 

! !INTERFACE:

  subroutine Run2 (GC, import, export, clock, RC)

    ! !ARGUMENTS:
    type (ESMF_GridComp), intent(inout) :: GC     ! Gridded component 
    type (ESMF_State),    intent(inout) :: import ! Import state
    type (ESMF_State),    intent(inout) :: export ! Export state
    type (ESMF_Clock),    intent(inout) :: clock  ! The clock
    integer, optional,    intent(  out) :: RC     ! Error code:

! !DESCRIPTION: Run2 method for the Dust Grid Component.

!EOP
!============================================================================
! Locals
    character (len=ESMF_MAXSTR)       :: COMP_NAME
    type (MAPL_MetaComp), pointer     :: MAPL
    type (ESMF_State)                 :: internal
    type (wrap_)                      :: wrap
    type (DU2G_GridComp), pointer     :: self

    integer                           :: import_shape(2), i2, j2
    integer                           :: n
    real, allocatable, dimension(:,:) :: drydepositionfrequency, dqa
!    real, dimension(:,:)     :: fluxout
    real                              :: fwet
    logical                           :: KIN
    

!   Varaibles for testing/dev - to be removed
!    real, dimension(:,:,:,:), allocatable:: DU
    real, pointer, dimension(:,:,:)   :: ptr3d_int

    real, parameter ::  von_karman = 0.4
    real, parameter ::  cpd    = 1004.16


#include "DU2G_DeclarePointer___.h"

    __Iam__('Run2')

!*****************************************************************************
!   Begin... 

!   Get my name and set-up traceback handle
!   ---------------------------------------
    call ESMF_GridCompGet (GC, NAME=COMP_NAME, __RC__)
    Iam = trim(COMP_NAME) // '::' // Iam

!if(mapl_am_i_root()) print*,trim(comp_name),' Run2 BEGIN'

!   Get my internal MAPL_Generic state
!   -----------------------------------
    call MAPL_GetObjectFromGC (GC, MAPL, __RC__)

!   Get parameters from generic state.
!   -----------------------------------
    call MAPL_Get (MAPL, INTERNAL_ESMF_STATE=internal, __RC__)

#include "DU2G_GetPointer___.h"

!   Get my private internal state
!   ------------------------------
    call ESMF_UserCompGetInternalState(GC, 'DU2G_GridComp', wrap, STATUS)
    VERIFY_(STATUS)
    self => wrap%ptr

    allocate(dqa, mold=wet1, __STAT__)
    allocate(drydepositionfrequency, mold=wet1, __STAT__)
!    allocate(fluxout, mold=wet1, __STAT__)

!   Get dimensions
!   ---------------
    import_shape = shape(wet1)
    i2 = import_shape(1)
    j2 = import_shape(2)


!   Dust Settling
!   -----------
!    call Chem_Settling2G(self%km, self%rhFlag, DU, MAPL_GRAV, delp, &
!                         self%radius*1.e-6, self%rhop, self%cdt, t, &
!                         airdens, rh2, zle, DUSD, rc, &
!                         correctionMaring=.true.)

    call Chem_Settling2Gorig (self%km, self%rhFlag, DU, MAPL_grav, delp, &
                              self%radius*1.e-6, self%rhop, self%cdt, t, airdens, &
                              rh2, zle, DUSD, rc, &
                              correctionMaring=.true.)

!do n=1,self%nbins
!if(mapl_am_i_root()) print*,'n = ', n,' : sum DUSD(n) = ', sum(DUSD(:,:,n))
!end do

!   Dust Deposition
!   ----------------
   do n = 1, self%nbins
      drydepositionfrequency = 0.
      call DryDeposition(self%km, t, airdens, zle, lwi, ustar, zpbl, sh,&
                         von_karman, cpd, MAPL_GRAV, z0h, drydepositionfrequency, rc, &
                         self%radius(n)*1.e-6, self%rhop(n), u10m, v10m, frlake, wet1)

      dqa = 0.
      dqa = max(0.0, DU(:,:,self%km,n)*(1.-exp(-drydepositionfrequency*self%cdt)))
      DU(:,:,self%km,n) = DU(:,:,self%km,n) - dqa

!if(mapl_am_i_root()) print*,'n = ', n,' : DU2G dqa = ',sum(dqa)
!if(mapl_am_i_root()) print*,'n = ', n,' : DU2G drydepf = ',sum(drydepositionfrequency)
!if(mapl_am_i_root()) print*,'n = ', n,' : DU2G sum(du00n) = ',sum(DU(:,:,:,n))

!    if( associated(DUDP) then
!       DU_dep(:,:,n) = dqa*delp(:,:,self%km)/MAPL_GRAV/self%cdt
!    end if
   end do

!  Dust Large-scale Wet Removal
!  ----------------------------
!#if 0
   KIN = .TRUE.
   do n = 1, self%nbins
      fwet = 0.3 ! Set this in DU2G_GridComp_DUxx.rc?

      call WetRemovalGOCART2G(self%km, self%nbins, self%nbins, n, self%cdt, 'dust', &
                              KIN, MAPL_GRAV, fwet, DU(:,:,:,n), ple, t, airdens, &
                              pfl_lsan, pfi_lsan, cn_prcp, ncn_prcp, DUWT, rc)
!      if (associated(DUWT)) DUWT(:,:,n) = fluxout

!if(mapl_am_i_root()) print*,'n = ', n,' : DU2G fluxout = ',sum(fluxout)
!if(mapl_am_i_root()) print*,'n = ', n,' : DU2G DUWT = ',sum(DUWT(:,:,n))
!if(mapl_am_i_root()) print*,'n = ', n,' : DU2G sum(du00n) = ',sum(DU(:,:,:,n))
   end do


   call DU_Compute_Diags ( self%diag_MieTable(self%instance), self%km, self%nbins, self%rlow, self%rup, &
                           self%diag_MieTable(self%instance)%channels, DU, MAPL_GRAV, t, airdens, &
                           rh2, u, v, delp, DUSMASS, DUCMASS, DUMASS, DUEXTTAU, DUSCATAU,     &
                           DUSMASS25, DUCMASS25, DUMASS25, DUEXTT25, DUSCAT25, &
                           DUAERIDX, DUFLUXU, DUFLUXV, DUCONC, DUEXTCOEF, DUSCACOEF,    &
                           DUEXTTFM, DUSCATFM , DUANGSTR, rc )



!if(mapl_am_i_root()) print*,'DU2G sum(DUCMASS25) = ',sum(DUCMASS25)
!if(mapl_am_i_root()) print*,'DU2G sum(DUMASS) = ',sum(DUMASS)
!if(mapl_am_i_root()) print*,'DU2G sum(DUSCATAU) = ',sum(DUSCATAU)
!if(mapl_am_i_root()) print*,'DU2G sum(DUANGSTR) = ',sum(DUANGSTR)


!do n = 1, self%nbins
! if(mapl_am_i_root()) print*,'n = ', n,' : DU2G sum(du00n) = ',sum(DU(:,:,:,n))
!end do




!if(mapl_am_i_root()) print*,trim(comp_name),' Run2 END'

    RETURN_(ESMF_SUCCESS)

  end subroutine Run2

!============================================================================
!BOP
! !IROUTINE: Run_data -- ExtData Dust Grid Component

! !INTERFACE:

  subroutine Run_data (GC, import, export, internal, RC)

    ! !ARGUMENTS:

    type (ESMF_GridComp), intent(inout) :: GC       ! Gridded component 
    type (ESMF_State),    intent(inout) :: import   ! Import state
    type (ESMF_State),    intent(inout) :: export   ! Export state
    type (ESMF_State),    intent(inout) :: internal ! Interal state
    integer, optional,    intent(  out) :: RC       ! Error code:

! !DESCRIPTION: Updates pointers in Internal state with fields from ExtData. 

!EOP
!============================================================================
! Locals
    character (len=ESMF_MAXSTR)        :: COMP_NAME
    type (MAPL_MetaComp), pointer      :: MAPL
    type (ESMF_State)                  :: aero 

    integer                            :: i, n
    character (len=ESMF_MAXSTR)        :: field_name

    real, pointer, dimension(:,:,:)    :: ptr3d_int
    real, pointer, dimension(:,:,:)    :: ptr3d_imp

    __Iam__('Run_data')


!*****************************************************************************
!   Begin... 

!   Get my name and set-up traceback handle
!   ---------------------------------------
    call ESMF_GridCompGet (GC, NAME=COMP_NAME, __RC__)
    Iam = trim(COMP_NAME) //'::'//Iam

if (mapl_am_I_root()) print*,trim(comp_name),' Run_data BEGIN'

    call ESMF_StateGet (export, trim(COMP_NAME)//'_AERO', aero, __RC__)
!    call ESMF_AttributeGet (aero, name='aerosol_names', itemCount=n, __RC__)

!   Update interal data pointers with ExtData
!   -----------------------------------------
    do i = 1, n
    write (field_name, '(A, I0.3)') 'du', i
        call MAPL_GetPointer (internal, NAME=trim(field_name), ptr=ptr3d_int, __RC__)
        call MAPL_GetPointer (import,  NAME='clim'//trim(field_name), ptr=ptr3d_imp, __RC__)

        ptr3d_int = ptr3d_imp
    end do


if (mapl_am_I_root()) print*,trim(comp_name),' Run_data END'


    RETURN_(ESMF_SUCCESS)

  end subroutine Run_data


!-------------------------------------------------------------------------------------
  subroutine aerosol_optics(state, rc)

    implicit none

!   !ARGUMENTS:
    type (ESMF_State)                                :: state
    integer,            intent(out)                  :: rc

!   !Local
    integer, parameter                               :: DP=kind(1.0d0)
    real, dimension(:,:,:), pointer                  :: ple, rh
    real(kind=DP), dimension(:,:,:), pointer         :: var
    real, dimension(:,:,:,:), pointer                :: q, q_4d
    integer, allocatable                             :: opaque_self(:)
    type(C_PTR)                                      :: address
    type(DU2G_GridComp), pointer                     :: self

    character (len=ESMF_MAXSTR)                      :: fld_name
    type(ESMF_Field)                                 :: fld
    character (len=ESMF_MAXSTR)                      :: COMP_NAME

    real(kind=DP), dimension(:,:,:), allocatable     :: ext_s, ssa_s, asy_s  ! (lon:,lat:,lev:)
    real, dimension(:,:,:), allocatable     :: x
    integer                                          :: instance
    integer                                          :: n, nbins, dims(4)
    integer                                          :: i1, j1, i2, j2, km
    integer                                          :: band, offset
    integer, parameter                               :: n_bands = 1

    integer :: i, j, k

    __Iam__('DU2G::aerosol_optics')


!   Begin... 

!   Mie Table instance/index
!   ------------------------
    call ESMF_AttributeGet (state, name='mie_table_instance', value=instance, __RC__)

!   Radiation band
!   --------------
    band = 0
    call ESMF_AttributeGet (state, name='band_for_aerosol_optics', value=band, __RC__)
    offset = band - n_bands

!   Pressure at layer edges 
!   ------------------------
    call ESMF_AttributeGet (state, name='air_pressure_for_aerosol_optics', value=fld_name, __RC__)
    call MAPL_GetPointer (state, ple, trim(fld_name), __RC__)

!    call MAPL_GetPointer (state, ple, 'PLE', __RC__)

    i1 = lbound(ple, 1); i2 = ubound(ple, 1)
    j1 = lbound(ple, 2); j2 = ubound(ple, 2)
                         km = ubound(ple, 3)

!   Relative humidity
!   -----------------
    call ESMF_AttributeGet (state, name='relative_humidity_for_aerosol_optics', value=fld_name, __RC__)
    call MAPL_GetPointer (state, rh, trim(fld_name), __RC__)

!    call MAPL_GetPointer (state, rh, 'RH2', __RC__)


    allocate(ext_s(i1:i2, j1:j2, km), &
             ssa_s(i1:i2, j1:j2, km), &
             asy_s(i1:i2, j1:j2, km), &
                 x(i1:i2, j1:j2, km),__STAT__)

    call ESMF_StateGet (state, 'DU', field=fld, __RC__) !add as attribute - dont hard code?
    call ESMF_FieldGet (fld, farrayPtr=q, __RC__)

    nbins = size(q,4)

    allocate(q_4d(i1:i2, j1:j2, km, nbins), __STAT__)

    do n = 1, nbins
       do k = 1, km
          x(:,:,k) = ((PLE(:,:,k) - PLE(:,:,k-1))*0.01)*(100./MAPL_GRAV)
          q_4d(:,:,k,n) = x(:,:,k) * q(:,:,k,n)
       end do
    end do

    call ESMF_AttributeGet(state, name='mieTable_pointer', itemCount=n, __RC__)
    allocate (opaque_self(n), __STAT__)
    call ESMF_AttributeGet(state, name='mieTable_pointer', valueList=opaque_self, __RC__)

    address = transfer(opaque_self, address)
    call c_f_pointer(address, self)

    call mie_ (self%rad_MieTable(instance), nbins, n_bands, offset, q_4d, rh, ext_s, ssa_s, asy_s, __RC__)

    call ESMF_AttributeGet (state, name='extinction_in_air_due_to_ambient_aerosol', value=fld_name, __RC__)
    if (fld_name /= '') then
        call MAPL_GetPointer (state, var, trim(fld_name), __RC__)
        var = ext_s(:,:,:)
    end if

    call ESMF_AttributeGet (state, name='single_scattering_albedo_of_ambient_aerosol', value=fld_name, __RC__)
    if (fld_name /= '') then
        call MAPL_GetPointer (state, var, trim(fld_name), __RC__)
        var = ssa_s(:,:,:)
    end if

    call ESMF_AttributeGet (state, name='asymmetry_parameter_of_ambient_aerosol', value=fld_name, __RC__)
    if (fld_name /= '') then
        call MAPL_GetPointer (state, var, trim(fld_name), __RC__)
        var = asy_s(:,:,:)
    end if

    deallocate(ext_s, ssa_s, asy_s, __STAT__)

    RETURN_(ESMF_SUCCESS)

  contains

!    subroutine mie_(mie_table, aerosol_names, nb, offset, q, rh, bext_, bssa_, gasym_, rc)
    subroutine mie_(mie_table, nbins, nb, offset, q, rh, bext_s, bssa_s, basym_s, rc)

    implicit none

    type(Chem_Mie),                intent(inout) :: mie_table        ! mie table
    integer,                       intent(in   ) :: nbins            ! number of bins
    integer,                       intent(in )   :: nb               ! number of bands
    integer,                       intent(in )   :: offset           ! bands offset 
    real,                          intent(in )   :: q(:,:,:,:)       ! aerosol mass mixing ratio, kg kg-1
    real,                          intent(in )   :: rh(:,:,:)        ! relative humidity
    real(kind=DP), intent(  out) :: bext_s (size(ext_s,1),size(ext_s,2),size(ext_s,3))
    real(kind=DP), intent(  out) :: bssa_s (size(ext_s,1),size(ext_s,2),size(ext_s,3))
    real(kind=DP), intent(  out) :: basym_s(size(ext_s,1),size(ext_s,2),size(ext_s,3))
    integer,                       intent(  out) :: rc

    ! local
    integer                           :: l
    real                              :: bext (size(ext_s,1),size(ext_s,2),size(ext_s,3))  ! extinction
    real                              :: bssa (size(ext_s,1),size(ext_s,2),size(ext_s,3))  ! SSA
    real                              :: gasym(size(ext_s,1),size(ext_s,2),size(ext_s,3))  ! asymmetry parameter

    __Iam__('DU2G::aerosol_optics::mie_')

     bext_s  = 0.0d0
     bssa_s  = 0.0d0
     basym_s = 0.0d0

     do l = 1, nbins
         call Chem_MieQuery(mie_table, l, real(offset+1.), q(:,:,:,l), rh, bext, gasym=gasym, ssa=bssa)

         bext_s  = bext_s  +             bext     ! extinction
         bssa_s  = bssa_s  +       (bssa*bext)    ! scattering extinction
         basym_s = basym_s + gasym*(bssa*bext)    ! asymetry parameter multiplied by scatering extiction 
     end do

     RETURN_(ESMF_SUCCESS)

    end subroutine mie_

  end subroutine aerosol_optics

!---------------------------------------------------------------------------------------

end module DU2G_GridCompMod

